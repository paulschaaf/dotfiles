#@IgnoreInspection BashAddShebang
# -*- mode: shell-script; compile-command: (concat "zcompile '" buffer-file-name "'"); outline-regexp: "^[\t ]*#[\t ]+[^=]\\|^# +\\|^[\t ]*..." -*-

# This script is run by interactive shells. It should contain commands to set up aliases, functions, options, key
# bindings, etc.
#
# 1) All:         /etc/zshenv,       $ZDOTDIR/.zshenv
# 2) Interactive: /etc/zshrc,        $ZDOTDIR/.zshrc
# 3) Login:       /etc/zlogin,       $ZDOTDIR/.zlogin
# ...
# 4) Login:       $ZDOTDIR/.zlogout, /etc/zlogout


# ======== Profiling
#echo Sourcing ${(%):-%N}
if (( $#zprof )); then
    zmodload zsh/zprof
    echo Running zprof from ${(%):-%N}
    unset zprof
fi


# ======== Shell Options
unsetopt \
    beep \
    clobber \
    flow_control \
    hup \
    notify \
    print_exit_value \
    rm_star_silent

setopt \
    auto_list \
    auto_menu \
    auto_param_slash \
    auto_pushd \
    auto_remove_slash \
    bang_hist \
    correct \
    extended_history \
    function_argzero \
    hash_cmds \
    hash_dirs \
    hist_find_no_dups \
    hist_ignore_dups \
    hist_ignore_space \
    hist_no_functions \
    hist_reduce_blanks \
    ignore_eof \
    inc_append_history \
    prompt_subst \
    pushd_silent \
    share_history


# ======== Exported Variables
export ESHELL=$SHELL
export FCEDIT=zed
export FIGNORE=.class:.o:\~:.stackdump:dump.txt:nohup.out
if (( ! EUID )); then
  HISTFILE=~/.history_root
else
  HISTFILE=~/.history
fi
export HISTSIZE=1000
export SAVEHIST=1000
export LESS=' --RAW-CONTROL-CHARS --hilite-unread --ignore-case --jump-target=2 --no-init --quit-if-one-screen --status-column -Ps%dt/%D ?f%f::STDIN:$'
export LESS_TERMCAP_mb=$fg_bold[green]
export LESS_TERMCAP_md=$fg_bold[green]
export LESS_TERMCAP_me=$reset_color
export LESS_TERMCAP_se=$reset_color
export LESS_TERMCAP_so=$fg_bold[yellow]
export LESS_TERMCAP_ue=$reset_color
export LESS_TERMCAP_us=${fg_bold[red]/m/;$color[underline]m} # insert underline markup

export LESSKEY=${HOME}/.less_${HOSTNAME}
export MANPAGER=less
export NVM_DIR=${HOME}/.nvm
export PAGER=less
export RIPGREP_CONFIG_PATH=~/etc/rc/ripgreprc
# export USE_COLOR=true

# Any punctuation char in WORDCHARS is considered a part of the adjacent word.
# The remaining punctuation chars are considered separate words, regardless of
# what may be adjacent:  !"#&'()+,./:;<=>?@[]`{|}
export WORDCHARS='|*?-~\\$%^'


# ======== Aliases
alias ..='pushd ..'
alias ...="pushd ../.."
alias cd=pushd
alias cores='inxi -C'
alias du='du -h'
inMacOS && alias home="printf '\e[3;0;0;1t\e[8;142;100t'" # move to upper left then resize terminal -- see https://invisible-island.net/xterm/ctlseqs/ctlseqs.html
inCygwin && alias open=explorer
inLinux && alias open=dolphin
alias pd=popd
alias rm='rm -i'
isRoot || alias root='sudo -u root ZDOTDIR=~pschaaf zsh -il'
alias term='echoVar TERM'
alias tree="tree -a --dirsfirst"
alias vpnc-connect='sudo vpnc-connect'
alias vpnc-disconnect='sudo vpnc-disconnect'
inMacOS && alias work="printf '\e[3;0;0;1t\e[8;106;100t'"


# ======== ls Aliases and Functions
export LS_COLORS='no=00:fi=00:di=01;34:ln=01;36:pi=40;33:so=01;35:bd=40;33;01:cd=40;33;01:or=01;37;41:mi=01;37;41:ex=01;32:*.btm=01;32:*.tar=01;31:*.tgz=01;31:*.tbz2=01;31:*.arc=01;31:*.arj=01;31:*.taz=01;31:*.lzh=01;31:*.lha=01;31:*.zip=01;31:*.z=01;31:*.Z=01;31:*.gz=01;31:*.bz2=01;31:*.bz=01;31:*.tz=01;31:*.rpm=01;31:*.jpg=01;35:*.jpeg=01;35:*.gif=01;35:*.bmp=01;35:*.xbm=01;35:*.xpm=01;35:*.png=01;35:*.tif=01;35:*.tiff=01;35'
export LS_FLAGS=-BCFbhH
inMacOS \
    && LS_FLAGS="${LS_FLAGS}G" \
    || LS_FLAGS="${LS_FLAGS}${LS_COLORS:+ --color}"

alias ls="ls ${LS_FLAGS}"
alias la='ls -A'
alias ll='ls -l'
alias lla='ls -Al'
alias llt='ls -lrt'
alias lr='ls -R'
autoload -Uz lsd
autoload -Uz lld
autoload -Uz lsda
alias llda='lsda -l'
autoload -Uz lsf


# ======== Var and List Processing
autoload -Uz echoList
autoload -Uz echoVar
alias classpath='echoList CLASSPATH'
alias grep='grep --extended-regexp --color'
alias fpath='echoList FPATH'
alias libpath='echoList LD_LIBRARY_PATH'
alias path='echoList PATH'
autoload -Uz rubylib


# ======== Functions and Aliases
autoload -Uz alert

inMacOS && [[ -s /usr/local/etc/profile.d/autojump.sh ]] && source /usr/local/etc/profile.d/autojump.sh
inLinux && [[ -s /etc/profile.d/autojump.sh ]] && source /etc/profile.d/autojump.sh

autoload -Uz colors && colors

inMacOS \
    && alias df='df -Phl' \
    || autoload -Uz df

autoload -Uz ddu

# function basename() { echo ${(F)@:t} }
# function dirname() { echo ${(F)@:h} }

inMacOS && function find() { [[ $1 == -* ]] && command find . ${*} || command find ${*} }

autoload -Uz frotz
autoload -Uz mkfunction

if (( $#inIntelliJ )); then
    autoload -Uz rg_intellij
    alias rg=rg_intellij
fi

# Change to the specified dir & remove starting place from dir stack
function pcd() { cd "$@" && popd +1 2>/dev/null }

# cd to the specified directory, or if it's a file, to the containing directory
function pushd() {
    if [[ -z "$1" ]]; then
        builtin pushd
    elif [[ -f "$1" ]]; then
        builtin pushd ${1:h}
    else
        builtin pushd ${1}
    fi
}

function settitle() { printf '\e]1;%s\a' ${*:-$GIT_REPO} }

autoload -Uz splitMp3
autoload -Uz splitFlac

function wcat()  { cat $(which $*) }
function wcd()   { cd $(which $*) }
function wfile() { file $(which ${@}) 2>/dev/null || echo \'${@}\' not found }

alias whence='whence -ac'
autoload -Uz whichjava
autoload -Uz zed                  && alias fned='zed -f'
autoload -Uz zrecompile


# ======== Key Bindings
bindkey -e # Use Emacs key bindings

bindkey "\033[17~"   kill-whole-line       # F6
bindkey "\033[18~"   copy-region-as-kill   # F7
bindkey "\033[19~"   yank                  # F8
bindkey "\033[20~"   history-incremental-search-backward # F9

bindkey "\033[1;5D"  backward-word         # ctrl_left
bindkey "\033[1;5C"  forward-word          # ctrl_right

bindkey "\033[1~"    beginning-of-line     # home
bindkey "\033[4~"    end-of-line           # end

bindkey "\M-C\M-5"   up-case-word
bindkey "\M-C\M-,"   down-case-word

bindkey "\033[3~"    delete-char           # delete
bindkey "^?"         backward-delete-char  # backspace

bindkey "\033[3;5~"  kill-word             # ctrl_delete
bindkey "^H"         backward-kill-word    # ctrl_backspace

case ${TERM} in
    cygwin*)
        bindkey "\033H"  backward-delete-char
        bindkey "\033?"  backward-kill-word
        ;;
    rxvt*)
        bindkey "^H"     backward-delete-char
        bindkey "\033?"  backward-kill-word
        ;;
    *)
        ;;
esac

# Macro Keys

bindkey -s "\033[21~"  ' exit\n'                  # F10
bindkey -s "\033[Z"    ' popd\n'                  # shift_tab
bindkey -s ""^_""      '\C-apath \C-e\C-m!$\011'  # ctrl_? prepends 'path'

#     if [[ -z "$CONNECTION" ]]; then
#         #bindkey -r "\C-h" 	# remove binding
#         #bindkey "\C-h\C-k" describe`-key-briefly	#
#     fi


# ======== Remote sessions
if (( $#SUDO_USER )); then
    export CONNECTION=sudo
    colored_connection=$fg[magenta]
elif (( $#SSH_TTY )); then
    export CONNECTION=ssh
    colored_connection=${fg[black]/m/;48;5;226m}
elif (( $#REMOTEHOST || $#REMOTEUSER )); then
    export CONNECTION=tlnt
    colored_connection=$fg_bold[red]
fi

(( $#colored_connection )) && colored_connection=" %{$colored_connection%}${CONNECTION}%{$reset_color%}"


# ======== The Prompt
if [[ -f ~/etc/rc/git/git_zshrc.sh ]]; then
    zrecompile ~/etc/rc/git/git_zshrc.sh
    source ~/etc/rc/git/git_zshrc.sh

    function git() {
        command git $*
        echo "$*" | grep --quiet 'checkout\|--edit-description' && updateGitVars
    }
fi

function updateDirs() {
    dirs=`dirs -p | awk 'NR == 2 {printf "[0;38;5;243m"}; {print}' ORS=' '`
}
(( $#dirs )) || updateDirs # unless dirs is already set, run updateDirs

autoload -Uz add-zsh-hook
add-zsh-hook chpwd updateDirs

export GIT_PS1_SHOWDIRTYSTATE=1

currentUser=${USER//pschaaf/}

if (( $#currentUser || $#CONNECTION )); then # unless it's simply pschaaf@localhost
    if isRoot; then
        hostColor=$fg_bold[white]$bg[red]
    else
        case ${UNAME}:${HOSTNAME} in
            CYGWIN*)
                hostColor=$fg_bold[white]$bg[green]
                ;;
            Darwin*)
                hostColor=$fg[green]$bg[black]
                ;;
            Linux*)
                hostColor=$fg[green]$bg[black]
                ;;
            Windows*)
                hostColor=$fg_bold[white]$bg[blue]
                ;;
            *)
                hostColor=$fg_bold[white]$bg[black]
                ;;
        esac
    fi

    # append the host machine, maybe preceeded by an @
    (( $#CONNECTION )) && currentUser=${currentUser}${currentUser:+@}'%m'

    currentUser=" %{$hostColor%}${currentUser}%{$reset_color%}"
    unset hostColor
fi

(( $#HOSTMESSAGE )) && HOSTMESSAGE=" $fg_bold[magenta]$bg[black]$HOSTMESSAGE%{$reset_color%}"

PS1="%{[4;38;5;235m%}\${(l.\${COLUMNS}.. .)}%{$reset_color%}
%T${currentUser}${HOSTMESSAGE} %{$fg_bold[yellow]%}%62>\>>\$dirs%<<%{$reset_color%}
\$GIT_STATUS%2(L.%{$fg_bold[cyan]%}%L_.)%{$fg_no_bold[cyan]%}%!%{$reset_color%}${colored_connection} \$GW_PROMPT%(!.#.$) "

PS2="(%{$fg_bold[magenta]%}%_%{$reset_color%}) %(!.#.$) "


# ======== Guidewire
# Bootstrap the huge nvm (Node version manager) function
alias node="nvm install 10.13.0 && unalias node && node"
alias nvm="echo Loading...; [[ -s ${NVM_DIR}/nvm.sh ]] && unalias nvm && source ${NVM_DIR}/nvm.sh && nvm"


# ======== Keychain
# Let's re-use ssh-agent and/or gpg-agent between logins
# /usr/bin/keychain ${HOME}/.ssh/id_rsa ${HOME}/.ssh/id_dsa
# source ${HOME}/.keychain/$HOSTNAME-sh
#if [[ $UID -gt 0 ]]; then  # if I'm not root
#    eval `keychain --eval --agents ssh ~/.ssh/id_rsa`
#fi


# ======== Zsh Compinstall
zstyle ':completion:*' expand prefix suffix
zstyle ':completion:*' file-sort name
zstyle ':completion:*' group-name ''
zstyle ':completion:*' ignore-parents parent ..
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-prompt '%SAt %p: Hit TAB for more, or the character to insert%s'
zstyle ':completion:*' menu select=0
zstyle ':completion:*' select-prompt '%SScrolling active: current selection at %p%s'
zstyle ':completion:*' squeeze-slashes true
zstyle ':zftp:' titlebar true
zstyle ':completion:*' verbose true
zstyle  :compinstall filename "${HOME}/etc/rc/$HOST.zsh"

# if the day of the year == the day the cache was last changed
# Load and initialize the completion system ignoring insecure directories with a
# cache time of 20 hours, so it should almost always regenerate the first time a
# shell is opened each day.
autoload -Uz compinit
_comp_files=(${ZDOTDIR:-$HOME}/.zcompdump(Nm-20))
if (( $#_comp_files )); then
    compinit -i -C
else
    compinit -i
    zrecompile ~/.zcompdump
fi
unset _comp_files

#    unalias  run-help 2>/dev/null && autoload -Uz run-help
zmodload -i zsh/complist


# ======== Debugging
# Move this heredoc around in the file to temporarily disable sections
# of code. Make sure to keep the opening tag first, and the closing tag
# against the left margin on a line by itself. Don't forget that any
# edits are lost if this file is re-generated.
: <<DEBUG_NO_EXEC
DEBUG_NO_EXEC
