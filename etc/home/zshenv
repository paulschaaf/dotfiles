#@IgnoreInspection BashAddShebang
# -*- mode: shell-script; compile-command: (concat "zcompile '" buffer-file-name "'"); outline-regexp: "^[\t ]*#[\t ]+[^=]\\|^# +\\|^[\t ]*..." -*-

# This script is run first by every type of zsh unless the -f option is set. It should contain commands to set the
# PATH and other important environment variables, but not those that produce output or assume the shell is attached
# to a tty.
#
# 1) All:         /etc/zshenv,       $ZDOTDIR/.zshenv
# 2) Interactive: /etc/zshrc,        $ZDOTDIR/.zshrc
# 3) Login:       /etc/zlogin,       $ZDOTDIR/.zlogin
# ...
# 4) Login:       $ZDOTDIR/.zlogout, /etc/zlogout


# ========
# ======== Profiling
#echo Sourcing ${(%):-%N}
if (( $#zprof )); then
    zmodload zsh/zprof
    echo Running zprof from ${(%):-%N}
    unset zprof
fi


# ========
# ======== Default Settings
umask 22
export UNAME=${UNAME:=`uname`}

alias inCygwin=false
alias inMacOS=false
alias inLinux=false
alias inMingW32=false
case ${UNAME} in
    CYGWIN)
        alias inCygwin=true
        ;;
    Darwin)
        alias inMacOS=true
        ;;
    Linux)
        alias inLinux=true
        ;;
    MINGW32_NT*)
        alias inMingW32=true
        ;;
    *)
    ;;
esac

if inMacOS; then
    (/sbin/ifconfig | egrep -q ' 192.168.') \
        && alias atHome=true \
        || alias atHome=false
fi

(( $UID )) && alias isRoot=false || alias isRoot=true

(( $#CD_TO )) && builtin cd "$CD_TO" && unset CD_TO


# ========
# ======== Exported Variables
export BROWSER=google-chrome-stable
export EDITOR=/usr/bin/emacs
inMacOS && EDITOR=/usr/local/bin/emacs
isRoot && EDITOR=${EDITOR}' -u pschaaf -nw'

[[ ${USER} != pschaaf ]] && alias emacs='emacs -u pschaaf'
export EMAIL=paul.schaaf@gmail.com
[[ "${TERMINAL_EMULATOR}" = "JetBrains-JediTerm" ]] && export inIntelliJ=true
inLinux && export IDEA_ROOT=/opt
export LC_ALL=en_US.UTF-8
export SCREENDIR=${SCREENDIR:=~/tmp}


# ========
# ======== Paths
# Keep only the first occurrence of duplicates
typeset -U CDPATH CLASSPATH FPATH fpath LD_LIBRARY_PATH PATH path RUBYLIB
export     CDPATH CLASSPATH FPATH       LD_LIBRARY_PATH PATH      RUBYLIB

CLASSPATH=${CLASSPATH:=.}
fpath=(~pschaaf/etc/zshfunctions ${fpath})

if inMacOS; then
    export JAVA_ROOT=/Library/Java/JavaVirtualMachines
    export JAVA8_HOME=${JAVA_ROOT}/java-8-jdk
    export JAVA11_HOME=${JAVA_ROOT}/amazon-corretto-11.jdk/Contents/Home
else
    export JAVA_ROOT=/usr/lib/jvm
    export JAVA8_HOME=${JAVA_ROOT}/java-8-jdk
    export JAVA16_HOME=/usr/lib32/jvm/java32-6-jdk
    export JAVA18_AMD64_HOME=${JAVA8_HOME}
    export JAVA11_HOME=${JAVA_ROOT}/java-11-amazon-corretto
fi

export JAVA_HOME=${JAVA11_HOME}

LD_LIBRARY_PATH=${LD_LIBRARY_PATH}${LD_LIBRARY_PATH:+:}/lib:/usr/lib:~/lib
path=(
    ~/bin
    ~/bin/quick-ssh
    ${JAVA_HOME}/bin
    ${path}
    /usr/X11R6/bin
    /opt/git-gw
)
RUBYLIB=${RUBYLIB}${RUBYLIB:+:}~/src/ruby:~/bin


# ========
# ======== OS-Specific
inCygwin && source ~pschaaf/etc/rc/zshenv.cygwin
if inMingW32; then
    # Change the driveLetter+colon combo into forwardSlash+driveLetter, swap
    # all slashes, replace semicolons with colons. We can't use cygpath.exe
    # for this because it collapses all references to $CYGROOT to /, which
    # doesn't work outside of Cygwin (since $CYGWIN won't be the root
    # directory)
    CYGROOT=`echo ${CYGROOT} | sed 's~\([a-zA-Z]\):~/\1~g; s~\\\\~/~g; s~;~:~g'`
    alias cygpath=${CYGROOT}/bin/cygpath.exe

    fixDOSPathIn ANT_HOME GIT_PATH JAVA_HOME
fi


# ========
# ======== Guidewire
if [[ -f ~/dotfiles/secrets ]]; then
    source ~/dotfiles/secrets
fi
export ARTIFACTORY_USERNAME=${USER}
export P4_HOME=/opt/p4v
export P4CLIENT=pschaaf_satori_1124
export TEAMCITY_USER=${USER}


# ========
# ======== Debugging
# Move this heredoc around in the file to temporarily disable sections
# of code. Make sure to keep the opening tag first, and the closing tag
# against the left margin on a line by itself.
: <<DEBUG_NO_EXEC
DEBUG_NO_EXEC
