#@IgnoreInspection BashAddShebang
# -*- mode: shell-script; compile-command: (concat "zcompile '" buffer-file-name "'"); outline-regexp: "^[\t ]*#[\t ]+[^=]\\|^# +\\|^[\t ]*..." -*-

# This script is run first by every type of zsh unless the -f option is set. It should contain commands to set the
# PATH and other important environment variables, but not those that produce output or assume the shell is attached
# to a tty.
#
# 1) All:         /etc/zshenv,       $ZDOTDIR/.zshenv
# 2) Login:       /etc/zprofile,     $ZDOTDIR/.zprofile
# 3) Interactive: /etc/zshrc,        $ZDOTDIR/.zshrc
# 4) Login:       /etc/zlogin,       $ZDOTDIR/.zlogin
# ...
# 5) Login:       $ZDOTDIR/.zlogout, /etc/zlogout

# ========
# ======== Profiling
( (($#debug)) || (($#trace)) ) && echo "\e[42m# ===== Sourcing ${(%):-%N}\e[0m"
(( $#trace )) && set -x || set +x

# if $zprof is set run the profiler
if (( $#zprof )); then
  zmodload zsh/zprof
  echo Running zprof from ${(%):-%N}
fi


# ========
# ======== Default Settings
umask 22
export UNAME=${UNAME:=$(uname)}
UNAME=${UNAME//_*/}
(( $#CD_TO )) && builtin cd "$CD_TO" && unset CD_TO


# ========
# ======== Recompile if necessary
autoload -Uz zrecompile
zrecompile ~/.zshenv ~/.zlogin ~/.zshrc ~pschaaf/etc/rc/zshenv_*


# ========
# ======== Paths
export JAVA_ROOT=/usr/lib/jvm
CLASSPATH=${CLASSPATH:=.}

if [ "${UNAME}" = "Darwin" ]; then
   export JAVA_ROOT=/Library/Java/JavaVirtualMachines
   export JAVA_HOME=${JAVA_ROOT}/amazon-corretto-23.jdk/Contents/Home
else
  export JAVA_HOME=/usr/lib/jvm/default
fi

export os_override_path=(~/.local/bin ~/bin ~/bin/quick-ssh ${JAVA_HOME}/bin)

# Keep only the first occurrence of duplicates
typeset -U CDPATH CLASSPATH FPATH fpath LD_LIBRARY_PATH os_override_path PATH path RUBYLIB
export     CDPATH CLASSPATH FPATH       LD_LIBRARY_PATH os_override_path PATH      RUBYLIB

# this enables the correct symlink override for one of inCygwin, inMacOS, inLinux, etc.
case ${UNAME} in
  CYGWIN)
    export HOME=/home/$USERNAME
    os_override_path=(~/bin/win ${os_override_path[@]} )
    # do not source the global rcs
    unsetopt GLOBAL_RCS
    ;;
  Darwin)
    export HOMEBREW_PREFIX="/opt/homebrew";
    export HOMEBREW_CELLAR="${HOMEBREW_PREFIX}/Cellar";
    export HOMEBREW_REPOSITORY=${HOMEBREW_PREFIX};
    export INFOPATH="${HOMEBREW_PREFIX}/share/info:${INFOPATH:-}";
    alias idea='/Applications/IntelliJ\ IDEA\ 2024.2.3\ CE.app/Contents/MacOS/idea'
    os_override_path=(
      ~/bin/macos
      ${os_override_path[@]}
      ${HOMEBREW_PREFIX}/bin
      ${HOMEBREW_PREFIX}/sbin/
     )
    fpath=(${HOMEBREW_PREFIX}/share/zsh/site-functions ${fpath})
    ;;
  Linux)
    os_override_path=(~/bin/linux ${os_override_path[@]})
    ;;
  *)
  ;;
esac

path=(
  ${os_override_path[@]}
  ${path[@]}
  /usr/X11R6/bin
  /opt/git-gw
)

(($#debug)) && echo PATH is && echo $PATH | tr ':' '\n' | cat -n | sed -E 's_/cygdrive__; s_(/c/Users/pschaaf|%HOME%)_~_'

fpath=(~pschaaf/etc/zshfunctions ${fpath[@]})

RUBYLIB=${RUBYLIB}${RUBYLIB:+:}~/src/ruby:~/bin

# ========
# ======== Exported Variables
export BROWSER=google-chrome-stable
inMacOS && export BROWSER=open
#inMacOS && export DISPLAY=:0
export EDITOR=/usr/bin/nano

[[ ${USER} != pschaaf ]] && alias emacs='emacs -u pschaaf'
export EMAIL=paul.schaaf@gmail.com

inLinux && export IDEA_ROOT=/opt
export PASTEL_COLOR_MODE=24bit
export TEMP=/tmp

# set all `locale` variables
export LC_ALL=en_US.UTF-8


# ========
# ======== OS-Specific
if inCygwin; then
  zrecompile ~/etc/rc/zshenv_cygwin
  source ~pschaaf/etc/rc/zshenv_cygwin
fi


# ========
# ======== Guidewire
if inMacOS; then
  export inGuidewire=true
fi

if (($#inGuidewire)); then
  zrecompile ~/etc/rc/zshenv_guidewire
  source ~pschaaf/etc/rc/zshenv_guidewire
fi
