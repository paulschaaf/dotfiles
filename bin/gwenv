#!/bin/zsh

allVars=(GW_APPCODE GW_APPNAME GW_PL_VERSION GW_PL_NUM GW_PLATFORM GW_STUDIO_VERSION GW_PORT GW_PORT_DEBUG GW_COLOR GW_ANSI_NAME GW_ANSI_PLATFORM GW_VERSION GW_TITLE)
namedVars=()

autoload -Uz colorLabel

[[ "$1" = "--setx" ]] && shift && set -x
[[ "$1" = "--all" ]] && shift && namedVars+=(${allVars[@]})

# if it's a customer build
if [[ -f project-version.properties ]]; then
  eval "$(sed -En '
    s/^product.code/GW_APPCODE/p;
    s/^product.name(.*)/GW_APPNAME\1 cust/p;
    s/^project-version=/GW_PL_VERSION/p;
    s/^studio-version/GW_STUDIO_VERSION/p
  ' project-version.properties modules/configuration/product.properties | sed '/./ { s/^/export /g; }')"

  if ((! $#GW_PORT)); then
      case ${GW_APPCODE} in
      ab)
        GW_PORT=8280
        ;;
      bc)
        GW_PORT=8580
        ;;
      cc)
        GW_PORT=8080
        ;;
      pc)
        GW_PORT=8180
        ;;
      esac
  fi
  export GW_PORT=$GW_PORT

# it's a dev build
elif [[ -f gradle.properties ]]; then
  eval "$(sed -En '
    s/^appCode/GW_APPCODE/p;
    s/^appName/GW_APPNAME/p;
    s/^version/GW_VERSION/p;
    s/^plVersion/GW_PL_VERSION/p;
    s/^jettyPort/GW_PORT/p;
    s/^studioVersion/GW_STUDIO_VERSION/p;
    s/^camelVersion=.*/GW_APPCODE=ccpi\nGW_APPNAME=CCPI\n/p
  ' gradle.properties | sed '/./ { s/^/export /g; }')"

  # or maybe this *is* platform
  # shellcheck disable=SC2155 # declare separately
  (($#GW_PL_VERSION)) || eval "$(sed -nE 's/^version=/export GW_PL_VERSION=/gp' gradle.properties 2>/dev/null)"
fi

if (($#GW_PL_VERSION)); then
  case ${GW_PL_VERSION} in
  #  4.*)
  #    export GW_PLATFORM=athena
  #    ;;
  #  5.*)
  #    export GW_PLATFORM=bedrock
  #    ;;
  #  6.*)
  #    export GW_PLATFORM=carbon
  #    ;;
  #  7.*)
  #    export GW_PLATFORM=diamond
  #    ;;
  #  8.*)
  #    export GW_PLATFORM=emerald
  #    ;;
    9.*)
      GW_PLATFORM=ferrite
      ;;
    10.*)
      GW_PLATFORM=granite
      ;;
    50.0.*)
      GW_PLATFORM=halite
      ;;
    50.1.*)
      GW_PLATFORM=Aspen
      ;;
    50.2.*)
      GW_PLATFORM=Banff
      ;;
    50.3.*)
      GW_PLATFORM=Cortina
      ;;
    50.4.*)
      GW_PLATFORM=Dobson
      ;;
    50.5.*)
      GW_PLATFORM=Elysian
      ;;
    50.6.*)
      GW_PLATFORM=Flaine
      ;;
    50.7.*)
      GW_PLATFORM=Garmisch
      ;;
    50.8.*)
      GW_PLATFORM=Hakuba
      ;;
    50.9.*)
      GW_PLATFORM=Innsbruck
      ;;
    50.10.*)
      GW_PLATFORM=Jasper
      ;;
    50.11.*)
      GW_PLATFORM=Kufri
      ;;
    50.12.*)
      GW_PLATFORM=Las\ LeÃ±as
      ;;
    50.13.*)
      GW_PLATFORM=MMMMMMM
      ;;
    *)
      GW_PLATFORM='???'
      ;;
  esac
  export GW_PL_NUM=${GW_PL_VERSION%%.*}
  GW_ANSI_PLATFORM=$(colorLabel black orange ${GW_PLATFORM})
  export GW_PLATFORM GW_ANSI_PLATFORM
fi

namedVars=(${namedVars:-$*})

if (( $#GW_APPNAME )); then
  # shellcheck disable=SC2153 # possible misspelling
  export GW_COLOR=${GW_COLORS[$GW_APPNAME]}
  export GW_ANSI_NAME
  GW_ANSI_NAME=$(colorLabel ${(Q)GW_COLOR%% *} ${(Q)GW_COLOR##* } ${GW_APPNAME})

  # if this file is being executed directly--i.e. NOT being sourced--default to reporting on all variables
  if [[ ! $ZSH_EVAL_CONTEXT =~ :file$ ]] && ((! $#namedVars )); then
    namedVars=(${allVars[@]})
  fi
fi

export GW_TITLE="${PWD//$HOME/~} (${GW_APPCODE} ${GW_VERSION})"

(($#GW_PORT)) && export GW_PORT_DEBUG=$(($GW_PORT + 10))

(($#namedVars)) && eval typeset -p "${namedVars[@]}" 2>/dev/null

unset allVars namedVars