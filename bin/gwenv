#!/bin/zsh

allVars=(GW_ANSI_NAME GW_APPCODE GW_APPNAME GW_COLOR GW_PL_VERSION GW_PORT GW_SKI_RELEASE GW_STUDIO_VERSION)
namedVars=()

autoload -Uz colorLabel

[[ "$1" = "--setx" ]] && shift && set -x

# if it's a customer build
if [[ -f project-version.properties ]]; then
  eval $(sed -En "
    s/^product.code/GW_APPCODE/p;
    s/^product.name(.*)/GW_APPNAME\1 cust/p;
    s/^project-version=([0-9.]*).*/GW_PL_VERSION=\1/p;
    s/^studio-version/GW_STUDIO_VERSION/p
  " project-version.properties modules/configuration/product.properties | sed '/./ { s/^/export /g; }')

  if ((! $#GW_PORT)); then
      case ${GW_APPCODE} in
      ab)
        GW_PORT=8280
        ;;
      bc)
        GW_PORT=8580
        ;;
      cc)
        GW_PORT=8080
        ;;
      pc)
        GW_PORT=8180
        ;;
      esac
  fi
  export GW_PORT=$GW_PORT

# it's a dev build
elif [[ -f gradle.properties ]]; then
  eval $(sed -En "
    s/^appCode/GW_APPCODE/p;
    s/^appName/GW_APPNAME/p;
    s/^(plVersion|integrationGatewayVersion)=(.*)/GW_PL_VERSION=\2/p;
    s/^jettyPort/GW_PORT/p;
    s/^studioVersion/GW_STUDIO_VERSION/p;
    s/^camelVersion=.*/GW_APPCODE=ccpi\nGW_APPNAME=CCPI\n/p
  " gradle.properties | sed '/./ { s/^/export /g; }')

  case ${GW_PL_VERSION} in
    50.1.*)
      GW_SKI_RELEASE=Aspen
      ;;
    50.2.*)
      GW_SKI_RELEASE=Banff
      ;;
    50.3.*)
      GW_SKI_RELEASE=Cortina
      ;;
    50.4.*)
      GW_SKI_RELEASE=Dobson
      ;;
    50.5.*)
      GW_SKI_RELEASE=Elysian
      ;;
    50.6.*)
      GW_SKI_RELEASE=Flaine
      ;;
    50.7.*)
      GW_SKI_RELEASE=Garmisch
      ;;
    50.8.*)
      GW_SKI_RELEASE=Hakuba
      ;;
    50.9.*)
      GW_SKI_RELEASE=Innsbruck
      ;;
  esac
  export GW_SKI_RELEASE=$(colorLabel black orange ${GW_SKI_RELEASE})
fi

namedVars=${@}

if (( $#GW_APPNAME )); then
  export GW_COLOR=${GW_COLORS[$GW_APPNAME]}
  export GW_ANSI_NAME=$(colorLabel ${(Q)GW_COLOR%% *} ${(Q)GW_COLOR##* } ${GW_APPNAME})

  # if this file is being executed directly--i.e. NOT being sourced--default to reporting on all variables
  if [[ ! $ZSH_EVAL_CONTEXT =~ :file$ ]] && ((! $#namedVars )); then
    namedVars=${allVars}
  fi
fi

(($#namedVars)) && eval typeset -p ${namedVars}

unset allVars namedVars