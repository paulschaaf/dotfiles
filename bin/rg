#!/bin/zsh

export debug
binary=$([ -f /usr/local/bin/rg ] && echo /usr/local/bin/rg || echo /usr/bin/rg)
command=()

(($#inIntelliJ)) && command+=(--column)

function help() {
   $binary --help
   echo
   echo 'GUIDEWIRE MACRO IGNORE FLAGS:               WILL IGNORE'
   echo '     --ignore-localizations                 display*.properties'
   echo '     --ignore-entities                      *.eti, *.eix, *.etx'
   echo '     --ignore-pcfs                          *.pcf'
   echo '     --ignore-tests                         /test/, /cc-test/, etc.'
   echo '     --ignore-typelists                     *.tti, *.tix, *.ttx'

   echo
   echo 'EXPANSION OPERATORS                         EXPANDS TO'
   echo '     --ignore-datamodel                     --ignore-entities --ignore-typelists'
   echo '     --ignore-ui                            --ignore-localizations --ignore-pcfs'
   exit
}

function addGlob() {
   # shellcheck disable=SC2034
   for pattern in "${@}"; do
      command+=(--glob ${(qq)pattern})
   done
}

args=($@)
while (($#args)); do
   #   (($#debug)) && echo -n '\n=== ' && typeset args
   firstArg=${args[1]}
   args=(${args[@]:1}) # pop the argument

   #   (($#debug)) && typeset firstArg
   case ${firstArg} in
      --debug)
         debug=true
         ;;
      --heading)
         command+=(--heading --color=never)
         ;;
      -h|--help)
         help
         ;;
      --ignore-datamodel)
         # append this meta-expansion
         args=(--ignore-entities --ignore-typelists ${args[*]})
         ;;
      --ignore-localizations)
         addGlob "!**/display*.properties"
         ;;
      --ignore-entities)
         addGlob "!**/*.eti" "!**/*.eix" "!**/*.etx"
         ;;
      --ignore-pcfs)
         addGlob "!**/*.pcf"
         ;;
      --ignore-tests)
         addGlob "!**/test/*" "!**/gtest/*" "!**/cc-test/*" "!**/cc-test-*" "!**/cc-behavior-test*" "!**/cc-predenali-test*" "!**/cc-rest-test*"
         ;;
      --ignore-typelists)
         addGlob "!**/*.tti" "!**/*.tix" "!**/*.ttx"
         ;;
      --ignore-ui)
         # append this meta-expansion
         args=(--ignore-localizations --ignore-pcfs ${args[*]})
         ;;
      *)
         command+=(${(qq)firstArg})
         ;;
   esac
done

if (($#command)); then
   echo ${binary} ${command[*]}; echo

   if (($#inIntelliJ)); then
      (($#debug)) && set -x
      # insert a space between the column number and the matching text
      eval ${binary} ${command[*]} | sed -E 's_(:.[[]0m[0-9]+.[[]0m:)_\1 _' | toFileUrl
   else
      (($#debug)) && set -x
      eval ${binary} ${command[*]}
   fi

fi
