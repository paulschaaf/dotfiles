#!/bin/zsh
# list all file extensions:
#   find * -type f -printf '%f\n' | grep -v '~$' | sed -n '/\./ { s/^.*\.//g; p; }' | sort -u | tee extensions

export debug expanded
binary=$([ -f /usr/local/bin/rg ] && echo /usr/local/bin/rg || echo /usr/bin/rg)

bizrules=('**/bizrules/*.xml')
test_dirs=(
   '**/*-test*/'
   '**/*-gunit-*/'
   '**/apitestbench*/'
   '**/gtest/'
   '**/pcftest/'
   '**/servertest/'
   '**/smoketests/'
   '**/test-*/'
   '**/test/'
   '**/testdata/'
   '**/testharness/'
   '**/tests/'
   '**/testsupport/'
)

function help() {
   local type_adds extensions patterns includes
   type_adds=$(sed -En 's/^--type-add=//p' $RIPGREP_CONFIG_PATH | sort)

   # summarize matching entries on one line, wrapping if necessary
   extensions=$(echo "$type_adds" | sed -En 's/^([^:]*):\*\..*$/\1, 	/p' | fmt -w $((COLUMNS)))

   # split matching lines into two columns
   patterns=$(echo "$type_adds" | sed -En "s/^([^:]*):(([^*]|.[^.])[^:]*)$/   \1 		\2/p")
   includes=$(echo "$type_adds" | sed -En "s/^([^:]*):include:(.*)$/   \1   		\2/p")

   cat <<HEREDOC
   --inIntelliJ     Mostly for testing
   --notIntelliJ

GUIDEWIRE MACROS:       INCLUDES
   --bizrules, no-bizrules
       '${(j:', ':)bizrules}'

   --test-dirs, no-test-dirs
    	'${(j:', ':)test_dirs}'

EXTENSION TYPES (use with --type= or --type-not=)
   ${extensions%,}

PATTERN TYPES           PATTERN
${patterns}

INCLUDE TYPES           INCLUDES
${includes//,/, }

HEREDOC
   exit
}

command=()

function addSwitch() {
   # shellcheck disable=SC2034
   for switch in "${@}"; do
      command+=("${switch}")
   done
}

function addGlobs() {
   local negate=
   # shellcheck disable=SC2034
   [ "$1" = "--negate" ] && negate=true && shift

   for pattern in "${@}"; do
      (($#negate)) && pattern="!${pattern}"
      addSwitch --glob "${(qq)${pattern}}"
   done
   expanded=true
}

(($#inIntelliJ)) && addSwitch --color=always

# shellcheck disable=SC1105
while (($#@)); do
   #   (($#debug)) && typeset firstArg
   case ${1} in
      --debug)
         debug=true
         ;;
      --dry-run)
         dry_run=true
         ;;
      --heading)
         addSwitch --heading --color=never
         expanded=true
         ;;
      -h|--help)
         $binary --help
         help
         ;;
      --bizrules)
         addGlobs "${bizrules[@]}"
         ;;
      --no-bizrules)
         addGlobs --negate "${bizrules[@]}"
         ;;
      --no-test-dirs)
         addGlobs --negate "${test_dirs[@]}"
         ;;
      --inIntelliJ)
         inIntelliJ=true
         ;;
      --notIntelliJ)
         unset inIntelliJ
         ;;
      --test-dirs)
         addGlobs "${test_dirs[@]}"
         ;;
      --type-list)
         ${binary} --type-list
         exit
         ;;
      -*)
         # don't enquote switches
         addSwitch "${1}"
         ;;
      *)
         addSwitch "${(qq)1}"
         ;;
   esac
   shift
done

(($#expanded)) && printf "\e[90mExpanded to => %s %s\e[0m\n"  "${binary}" "${command[*]}"

if (($#dry_run)); then
   exit
elif (($#inIntelliJ)); then
   (($#debug)) && set -x
   # turn dash-number-dash into colon-number-colon, put a space after the last coordinate colon
   eval "${binary} ${command[*]}" | sed -E 's/-(.\[0m([0-9]+).\[0m)-/:\1:/; s_(^.*:.\[0m([0-9]+).\[0m):_\1 _' | toFileUrl
else
   (($#debug)) && set -x
   eval ${binary} ${command[*]}
fi
