#!/bin/zsh

unset choose debug setx

while (($#*)); do
  case $1 in
    --debug)#
      # shellcheck disable=SC2034
      debug=true
      ;;
    --choose)# prompt the user to select one
      # add this to .zshrc: alias gwcd='cd "$(gwls --choose)"'
      choose=true
      ;;
    --setx)# turn on tracing
      setx=true
      ;;
    *)
      echo Illegal argument: "$1"
      exit 1
      ;;
  esac
  shift
done

branch_width=75
version_width=18

repos=()
sentinel='0'
formatStr="%-2s %-${branch_width}s %-${version_width}s %s"

# display column headers on stderr so they don't get piped to the next command
(($#choose)) && echo -n '#  ' >&2 # header for the number column when the list is displayed using 'select'
printf "${formatStr}\n" '  ' 'Branch' 'Version' 'Dir' >&2

(($#setx)) && set -x

for dir in ~/gw/*; do
  if [[ -d ${dir}/.git ]]; then
    pushd ${dir} || exit 1

    dir="${PWD//${HOME}/~}"
    (($#debug)) && echo --- $dir >&2

    source gwenv --quiet
    appCode=${GW_ANSI_APPCODE:--}
    version=${GW_VERSION:--}
    branch=$(git rev-parse --abbrev-ref HEAD); branch=${branch//user?$USER/@}

    repo=$(printf ${formatStr} ${appCode} ${branch} ${version} ${sentinel}${dir})
    (($#choose)) && repos+=(${repo}) || echo $repo
    popd || exit 1
  fi
done

if (($#choose)); then
  select repo in ${repos}; do
    dir=${repo##*${sentinel}}
    (($#debug)) && typeset repo dir >&2
    echo ${dir//\~/${HOME}}
    break
  done
fi
