#!/bin/zsh

playerHost=copernicus
player=strawberry
thisFile=${(F)0:t}

if [[ "$1" == "--setx" ]]; then
  shift
  set -x
fi

if [[ `hostname` = "${playerHost}" ]]; then
  (( $# )) && playerctl -p $player "${@}" # arguments are a command

  if [[ "${@}" = "stop" ]]; then
    RESPONSE='message="Stopped"'
  else
    RESPONSE=$(playerctl ${player:+-p} ${player} metadata --format 'artist="{{xesam:artist}}"; song="{{xesam:title}}"; album="{{xesam:album}}"; track="{{xesam:trackNumber}}"; year="{{year}}"; playStatus="{{lc(status)}}"; position="{{duration(position)}}"; length="{{duration(mpris:length)}}"; typeset -i rating; let rating="((5*{{xesam:userRating}}))"; stars=$(print -r -- "${(pl:$rating::★:)}${(pl:((5-$rating))::☆:)}")' 2>/dev/null || echo message='Error: No track is selected')
  fi

else
  RESPONSE=$(ssh -x ${playerHost} ${thisFile} "${@}" || echo message="Error: Cannot reach $playerHost")
fi

echo $RESPONSE; echo
eval $RESPONSE

# shellcheck disable=SC2154 # var referenced but not assigned
if (($#message)); then
  title=${thisFile}

else
  title="${title:-$song} - ${stars:-??}"
  subtitle=${artist}
  track=${track:-?}
  year=${year:-?}
  album=${album:-unkn}

  message="\"$album\" [$year]
$position / $length ($playStatus track ${track})"
  [[ $UNAME = 'Darwin' ]] && message="\\"${message}
fi

if [[ $UNAME = 'Darwin' ]]; then
  cmd=(terminal-notifier -title ${title} ${subtitle:+-subtitle} ${subtitle} ${message:+-message} ${message})
  ${cmd[*]} >/dev/null &
  echo ${(q)cmd}
else
  notify-send -a ${title} "$subtitle
$message"
fi