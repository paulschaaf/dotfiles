#!/usr/bin/env zsh
allProducts=(ab bc cc ccpi pc px)
typeset -A selectedProducts

switches=()
alias dry_run=false
while [[ -n "$*" ]]; do
  switch=${1%%=*}
  [[ ${1} =~ "=" ]] && value=${1#*=} || value=''
  (($#debug)) && echo "Processing switch '$switch', value '$value'"
  case ${switch} in
    --debug)
      debug=true
      ;;
    --dry_run | --dry-run)
      alias dry_run=true
      ;;
    --all)
      for product in $allProducts; do
        selectedProducts[$product]=$product
      done
      ;;
    --root)
      root=$value
      ;;
    -*)
      product=${switch##--}
      if (($allProducts[(Ie)$product])); then # if it's a product
        # either add the product (e.g. "--ab"), or override the assumed directory (e.g. "--cc=cc2")
        selectedProducts[$product]=${value:-$product}
        (($#debug)) && typeset -p selectedProducts
      else # it's a normal switch
        switches+=(${1})
      fi
      ;;
    *) # it's a normal argument
      break
      ;;
  esac
  shift
done

# default associative array to cc and px
(($#selectedProducts)) || selectedProducts=(cc cc px px)
(($#debug)) && typeset -p selectedProducts

for prod in $selectedProducts; do
  cd ~
  prodDir=~/gw/${root:=halite}/${selectedProducts[$prod]}
  cd $prodDir
  source ~/bin/gwenv

  name="â–¶ ${GW_APPNAME}"
  color=${GW_ANSI_NAME//$GW_APPNAME*/}
  command=(rg --color=always ${switches} ${*} $prodDir)

  (($#debug)) && echo "\n===" && typeset -p color command name prod prodDir
  (($#debug)) && prodDir="[7m${command}"

  printf "\n${color}%s%$(($COLUMNS - $#name - 4))s[30;7mî‚´[0m\n" $name "${prodDir//$HOME/~}"

  dry_run || $command
done